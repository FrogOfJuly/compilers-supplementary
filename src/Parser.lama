-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- fun binOp(op_symbol) {
--   [s (op_symbol), fun(lhs, _, rhs) { Binop (op_symbol, lhs ,rhs) }]
-- }

fun binOp(symb) {
  [s(symb), fun (l, op, r) {Binop(symb, l, r)}]
}

-- Primary expression
var primary = memo $ eta syntax ( x=decimal{Const (stringInt (x))} 
                                | x=lident {Var (x)}               
                                | inbr[s("("), exp, s(")")]
    ),
    exp     = memo $ eta (expr({[Left, binOp.map({"!!"})],
                                [Left, binOp.map({"&&"})],
                                [Nona, binOp.map({"<", ">", "<=", ">=", "==", "!="})],
                                [Left, binOp.map({"+", "-"})],
                                [Left, binOp.map({"/", "*", "%"})]}, primary)
    );

var stmt     = memo $ eta syntax ( one_stmt     
                                 | l=one_stmt s[";"] r=stmt {Seq (l, r)}
    ),
    one_stmt = memo $ eta syntax ( kRead x=inbr[s("("), lident, s(")")]                     {Read (x)} 
                                 | kWrite e=inbr[s("("), exp, s(")")]                       {Write (e)} 
                                 | kIf res=if_stmt                                          {res}
                                 | kWhile c=exp kDo b=parse kOd                             {While (c, b)}
                                 | kDo b=parse kWhile c=exp kOd                             {DoWhile (b, c)}
                                 | kSkip                                                    {Skip} 
                                 | kFor i=parse s[","] c=exp s[","] n=parse kDo b=parse kOd {Seq (i, While (c, Seq (b, n)))}
                                 | x=lident s[":="] e=exp                                   {Assn(x, e)}
    ),
    if_stmt  = memo $ eta syntax ( c=exp kThen t=parse e=else_if {If (c, t, e)}),
    else_if  = memo $ eta syntax ( kElse body=parse kFi {body}
                                 | kElif b=if_stmt      {b}
                                 | kFi                  {Skip}                          
    );
    
-- Public top-level parser
public parse = stmt;
             
